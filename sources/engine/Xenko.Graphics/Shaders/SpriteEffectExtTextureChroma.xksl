// Copyright (c) Xenko contributors (https://xenko.com) and Silicon Studio Corp. (https://www.siliconstudio.co.jp)
// Distributed under the MIT license. See the LICENSE.md file in the project root for more information.
shader SpriteEffectExtTextureChroma : SpriteEffectExtTexture
{	
	stage float Similarity = 0;
    stage float Smoothness = 0;
    stage float Spill = 0;
	[Color]
	stage float3 ChromaKeyColor = float3(0.0, 0.0, 0.0);

    float2 RGBtoUV(float3 rgb) {
        return float2(
            rgb.r * -0.169 + rgb.g * -0.331 + rgb.b * 0.5 + 0.5,
            rgb.r * 0.5 + rgb.g * -0.419 + rgb.b * -0.081 + 0.5
        );
    }

    stage override void PSMain()
    {
        float4 color = Shading();
        float chromaDist = distance(RGBtoUV(color), RGBtoUV(ChromaKeyColor));

        float baseMask = chromaDist - Similarity;
        float fullMask = pow(saturate(baseMask/ Smoothness), 1.5);

        color.a = fullMask;

        float spillVal = pow(clamp(baseMask / Spill, 0, 1), 1.5);
        float desat = saturate(color.r * 0.2126 + color.g * 0.7152 + color.b * 0.0722);
        color.rgb = lerp(float3(desat, desat, desat), color.rgb, spillVal);

		streams.ColorTarget = color;
    }
};
